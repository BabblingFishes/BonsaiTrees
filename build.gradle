buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod.version}"
group = "org.dave.bonsaitrees" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "bonsaitrees"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
    runDir = "run"

    mappings = "snapshot_20170811"
}

repositories {
    maven { // Waila
        name "Mobius Repo"
        url "http://tehnut.info/maven"
    }

    maven { // TheOneProbe
        name 'tterrag maven'
        url "http://maven.tterrag.com/"
    }

    maven { // JEI
        name "Progwml Repo"
        url "http://dvs1.progwml6.com/files/maven"
    }

    maven {
        name = "Forestry"
        url = "http://maven.ic2.player.to/"
    }

    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}:api"
    runtime "mezz.jei:jei_${config.minecraft.version}:${config.jei.version}"

    deobfCompile('net.sengir.forestry:forestry_1.12.2:5.5.6.197:api') {
        transitive = false
    }
    runtime('net.sengir.forestry:forestry_1.12.2:5.5.6.197') {
        transitive = false
    }

    deobfCompile "mcp.mobius.waila:Hwyla:${config.waila.version}:api"

    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.18-10") {
        transitive = false
    }
}

allprojects {
    if (System.getenv("TRAVIS") != null) {
        def tag = System.getenv("TRAVIS_TAG");
        if (tag != null && !tag.isEmpty() && tag.contains("release")) {
            project.ext.releaseType = "release"
        } else {
            version += "-b${System.getenv("TRAVIS_BUILD_NUMBER")}"
            project.ext.releaseType = "beta"
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
